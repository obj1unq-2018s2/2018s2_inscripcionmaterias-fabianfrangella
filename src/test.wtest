import materia.*
import estudiante.*
import universidad.*
describe "inscripciones"{
		
	const informatica = new Carrera ()
	
	var fabi = new Estudiante (
		carreras=#{informatica},materiasAprobadas = #{},materiasInscriptas=#{}
	)
	var fede = new Estudiante (
		carreras=#{informatica},materiasAprobadas = #{},materiasInscriptas=#{}
	)
	var andy = new Estudiante (
		carreras=#{informatica},materiasAprobadas = #{},materiasInscriptas=#{}
	)	
	var dami = new Estudiante (
		carreras=#{informatica},materiasAprobadas = #{},materiasInscriptas=#{}
	)
	
	const intro = new Materia(carrera=informatica,creditos=8,anio=1, cupo = 20)
	
	const objetos1 = new MateriaConCorrelativas(
		carrera=informatica,creditos=10,anio=2,correlativas = #{intro},cupo = 20
	)
	
	const objetos1Bis = new MateriaConCorrelativas(
		carrera=informatica,creditos=10,anio=2,correlativas = #{intro},cupo = 1
	)
	
	const redes = new MateriaElitista(carrera=informatica,creditos=10,anio=2,cupo=1)
	
	const estrategias = new MateriaPorGradoDeAvance (carrera=informatica,creditos=12,anio=3,cupo=1)
	
	
	
	fixture{
		informatica.asignarMateria(intro)
		informatica.asignarMateria(objetos1)
		informatica.asignarMateria(objetos1Bis)
		fabi.registrarAprobada(intro,8)
		fede.registrarAprobada(intro,10)
		andy.registrarAprobada(intro,9)
		objetos1Bis.inscribir(fabi)
		objetos1Bis.inscribir(fede)
		redes.inscribir(fabi)
		redes.inscribir(fede)
		redes.inscribir(andy)
		estrategias.inscribir(fabi)
		estrategias.inscribir(fede)
		estrategias.inscribir(dami)
		
		
	}

	test "las notas no se cargan dos veces"{
		fabi.registrarAprobada(intro,7)
		assert.that(fabi.materiasAprobadas().size()==1)
	}
	test "inscriptos" { 
		assert.equals(objetos1Bis.alumnos(),#{fabi})
	}
	
	test "aprobo el anio anterior"{
		assert.that(fabi.aproboElAnioAnterior(objetos1))
	}
	
	test "dar de baja un pibe y que se anote el que esta en espera"{
		objetos1Bis.darDeBaja(fabi)
		assert.equals(objetos1Bis.alumnos(),#{fede})
	}
	
	test "estudiantes inscriptos en objetos1"{
		objetos1.inscribir(fabi)
		objetos1.inscribir(fede)
		assert.equals(objetos1.alumnos(),#{fabi,fede})
	}
	
	test "estudiantes en lista de espera de objetos 1"{
		assert.equals(objetos1Bis.estudiantesEnListaDeEspera(),[fede])
	}
	
	test "materias que puede cursar de informatica"{
		assert.equals(fabi.materiasQuePuedeCursar(informatica),#{objetos1,objetos1Bis})
	}
	
	test "materias a las que esta inscripto fabi"{
		assert.equals(fabi.materiasInscriptas(),#{objetos1Bis,estrategias,redes})
	}
	
	test "materias en las que fede esta en espera"{
		assert.equals(fede.materiasEnEspera(),#{objetos1Bis,estrategias,redes})
	}
	
	test "fede quiere cursar una aprobada"{
		assert.throwsExceptionWithMessage("No podes anotarte en esta materia",{intro.inscribir(fede)})
	}
	
	test "dami quiere anotarse en objetos y no tiene la correlativa"{
		assert.throwsExceptionWithMessage("No podes anotarte en esta materia",{objetos1.inscribir(dami)})
	}
	
	test "andy queda en lista de espera para objetos" {
		objetos1Bis.inscribir(andy)
		assert.equals(andy.materiasEnEspera(),#{redes,objetos1Bis})
		assert.equals(objetos1Bis.estudiantesEnListaDeEspera(),[fede,andy])
	}
	
	test "se anota en estrategias fede porque es el de mas materias metidas en la lista de espera"{
		estrategias.darDeBaja(fabi)
		assert.equals(estrategias.alumnos(),#{fede})
	}
	
	test "se anota en redes fede por mejor promedio"{
		redes.darDeBaja(fabi)
		assert.equals(redes.alumnos(),#{fede})
	}
	
}